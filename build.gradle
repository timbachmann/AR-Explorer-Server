buildscript {
    ext.kotlinVersion = '1.4.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'idea'

group 'de.timbachmann'
version '1.0'

repositories {
    mavenCentral()
}

mainClassName = 'de.timbachmann.capvisar.rest.MainKt'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

jar {
    manifest {
        attributes 'Version': archiveVersion.get()
        attributes 'Main-Class': mainClassName // Same as for the application plugin.
        attributes 'Manifest-Version': '1.0'
        attributes 'Multi-Release': true
    }

    // Gradle 7.0 requires a duplicate handling strategy or fails if duplicates occur.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude 'META-INF', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

dependencies {

    def jettyVersion = '9.4.44.v20210927'
    def alpnApiVersion = '1.1.3.v20160715'
    def alpnBootVersion = '8.1.12.v20180117'

    implementation group: 'io.javalin', name: 'javalin', version: '4.1.0'
    implementation group: 'io.javalin', name: 'javalin-openapi', version: '4.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.31"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"

    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "2.17.0"
    implementation group: 'io.github.microutils', name: 'kotlin-logging-jvm', version: "2.1.16"

    implementation group: 'org.litote.kmongo', name: 'kmongo-serialization', version: "4.5.0"
    implementation group: 'org.litote.kmongo', name: 'kmongo-id-serialization', version: "4.5.0"

    implementation group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.1.5'
    implementation group: 'org.webjars', name: 'swagger-ui', version: '3.24.3'

    implementation group: 'org.eclipse.jetty.http2', name: 'http2-server', version: jettyVersion
    implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-server', version: jettyVersion
    implementation group: 'org.eclipse.jetty.alpn', name: 'alpn-api', version: alpnApiVersion
    implementation group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: alpnBootVersion

    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'

    implementation("net.coobird:thumbnailator:0.4.17")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}
